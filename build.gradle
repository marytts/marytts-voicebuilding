plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
}

group 'de.dfki.mary'
version '0.2-SNAPSHOT'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()

    exclusiveContent {
        forRepository {
            maven {
                url 'https://mlt.jfrog.io/artifactory/mlt-mvn-releases-local'
            }
        }
        filter {
            includeGroup 'de.dfki.lt.jtok'
        }
    }
}

dependencies {
    implementation group: 'de.dfki.mary', name: 'marytts-runtime', version: maryttsVersion, {
        exclude group: 'com.twmacinta', module: 'fast-md5'
        exclude group: 'gov.nist.math', module: 'Jampack'
    }
    testImplementation group: 'org.testng', name: 'testng', version: '7.5'
    testImplementation group: 'xmlunit', name: 'xmlunit', version: '1.6'
    testRuntimeOnly group: 'de.dfki.mary', name: 'voice-cmu-slt-hsmm', version: maryttsVersion, {
        transitive = false
    }
    testRuntimeOnly group: 'de.dfki.mary', name: 'marytts-lang-en', version: maryttsVersion, {
        transitive = false
    }
    testRuntimeOnly group: 'de.dfki.mary', name: 'marytts-lang-de', version: maryttsVersion, {
        transitive = false
    }
}

test {
    useTestNG()
    workingDir = buildDir
    testLogging {
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name 'OSSRH'
            url isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    : 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}
