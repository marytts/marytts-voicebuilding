plugins {
    id 'base'
    id 'groovy'
    id 'maven-publish'
}

group 'de.dfki.mary'
version '0.2-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.1'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.10'
    compile group: 'de.dfki.mary', name: 'marytts-runtime', version: maryttsVersion
    testCompile group: 'org.testng', name: 'testng', version: '6.9.4'
    testCompile group: 'xmlunit', name: 'xmlunit', version: '1.6'
    testRuntime group: 'de.dfki.mary', name: 'voice-cmu-slt-hsmm', version: maryttsVersion
    testRuntime group: 'de.dfki.mary', name: 'marytts-lang-de', version: maryttsVersion
}


configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            force 'org.codehaus.groovy:groovy-all:2.5.10'
            substitute module('org.slf4j:slf4j-log4j12:1.6.1') with module('org.slf4j:log4j-over-slf4j:1.6.1')
        }
    }
}


test {
    useTestNG()
    workingDir = buildDir
    testLogging {
        exceptionFormat = 'full'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/marytts'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
