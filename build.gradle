plugins {
    id 'groovy'
    id 'maven-publish'
}

group 'de.dfki.mary'
version '0.2-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

repositories {
    mavenCentral()

    exclusiveContent {
        forRepository {
            maven {
                url 'https://mlt.jfrog.io/artifactory/mlt-mvn-releases-local'
            }
        }
        filter {
            includeGroup 'de.dfki.lt.jtok'
        }
    }
}

dependencies {
    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.4'
    implementation group: 'de.dfki.mary', name: 'marytts-runtime', version: maryttsVersion, {
        exclude group: 'com.twmacinta', module: 'fast-md5'
        exclude group: 'gov.nist.math', module: 'Jampack'
    }
    testImplementation group: 'org.testng', name: 'testng', version: '6.9.4'
    testImplementation group: 'xmlunit', name: 'xmlunit', version: '1.6'
    testRuntimeOnly group: 'de.dfki.mary', name: 'voice-cmu-slt-hsmm', version: maryttsVersion
    testRuntimeOnly group: 'de.dfki.mary', name: 'marytts-lang-de', version: maryttsVersion
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            force 'org.codehaus.groovy:groovy-all:2.4.4'
            substitute module('org.slf4j:slf4j-log4j12:1.6.1') with module('org.slf4j:log4j-over-slf4j:1.6.1')
        }
    }
}


test {
    useTestNG()
    workingDir = buildDir
    testLogging {
        exceptionFormat = 'full'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/marytts'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
